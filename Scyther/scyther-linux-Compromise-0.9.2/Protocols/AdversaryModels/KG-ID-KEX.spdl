/* 
 * Boyd et al 2007
 * Kiltz-Galindo KEM based two pass version
 *
 */

// Hash functions
hashfunction h1,h2,g1,g2,exp,KDF,LSB;
hashfunction exp;

const g;

/*
 * Hack to simulate g^ab = g^ba inside terms.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA)
{
	role RA
	{
		var X,Y: Ticket;

		recv_!1(RA,RA, exp(exp(g,X),Y) );
		send_!1(RA,RA, exp(exp(g,Y),X) );
	}
}

// The protocol description

symmetric-role protocol KG-ID-KEX(I,R)
{
	role I
	{
		fresh x: Nonce;
		var Y: Ticket;

		send_1(I,R, { exp(g,x) }pk(R) );
		recv_2(R,I, { Y }pk(I) );

		claim(I,SKR, exp(Y,x) );
	}	
	
	role R
	{
		fresh y: Nonce;
		var X: Ticket;

		recv_1(I,R, { X }pk(R) );
		send_2(R,I, { exp(g,y) }pk(I) );

		claim(R,SKR, exp(X,y) );
	}
}

