/* 
 * Two-move Diffie-Hellman according to Shmatikov, Gupta
 * Naxossed
 */

// Hash functions
hashfunction h1,h2,g1,g2;

/*
 * Hack to simulate g^ab = g^ba.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA)
{
	role RA
	{
		var alpha,beta, T1,T2: Ticket;

		recv_!1(RA,RA, g2(g1(T1),T2) );
		send_!2(RA,RA, g2(g1(T2),T1) );
	}
}

// The protocol description

protocol DH-GS-naxos(I,R)
{
	role I
	{
		fresh x: Nonce;
		var i: Nonce;
		var beta: Ticket;

		send_Compromise(I,I, h1(sk(I),x) );

		send_1(I,R,  g1(h1(sk(I),x)),R,         { g1(h1(sk(I),x)),R }sk(I)        );
		recv_2(R,I,  g1(h1(sk(I),x)),beta,i,I,  { g1(h1(sk(I),x)),beta,i,I }sk(R) );

		claim(I,SKR, g2(beta,h1(sk(I),x)) );
	}	
	
	role R
	{
		fresh y: Nonce;
		fresh i: Nonce;
		var alpha: Ticket;

		send_Compromise(R,R, h1(sk(R),y),i );

		recv_1(I,R,  alpha,R,          { alpha,R }sk(I)         );
		send_2(R,I,  alpha,g1(h1(sk(R),y)),i,I,  { alpha,g1(h1(sk(R),y)),i,I }sk(R) );

		claim(R,SKR, g2(alpha,h1(sk(R),y)) );
	}
}

