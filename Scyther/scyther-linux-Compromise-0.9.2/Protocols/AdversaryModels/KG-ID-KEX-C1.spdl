/* 
 * Boyd et al 2007
 * Kiltz-Galindo KEM based two pass version
 *
 * Cas mod 1
 *
 */

// Hash functions
hashfunction h1,h2,g1,g2,exp,KDF,LSB;
hashfunction exp,KDF;

const g;

/*
 * Hack to simulate g^ab = g^ba inside terms.
 * '@' prefix of protocol name denotes helper protocol, which is used by
 * Scyther for displaying, and such protocols are ignored in
 * auto-generation of protocol modifiers.
 */
protocol @exponentiation(RA,RB)
{
	role RA
	{
		var X,Y: Ticket;

		recv_!1(RA,RA, exp(exp(g,X),Y) );
		send_!2(RA,RA, exp(exp(g,Y),X) );
	}
	role RB
	{
		var X,Y: Ticket;

		recv_!3(RB,RB, KDF(exp(exp(g,X),Y),RA,RB) );
		send_!4(RB,RB, KDF(exp(exp(g,Y),X),RA,RB) );
	}
}

// The protocol description

symmetric-role protocol KG-ID-KEX-C1(I,R)
{
	role I
	{
		fresh x: Nonce;
		var Y: Ticket;

		send_1(I,R, { exp(g,x) }pk(R) );

		send_Compromise(I,I, x);

		recv_2(R,I, { Y }pk(I) );

		claim(I,SKR, KDF(exp(Y,x),I,R) );
	}	
	
	role R
	{
		fresh y: Nonce;
		var X: Ticket;

		recv_1(I,R, { X }pk(R) );
		send_Compromise(R,R, R);
		send_2(R,I, { exp(g,y) }pk(I) );

		claim(R,SKR, KDF(exp(X,y),I,R) );
	}
}

