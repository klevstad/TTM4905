/*
Secure Authentication and Key Establishment Scheme (SAKES) - Authentication - Improved
*/

hashfunction MAC; # Message Authentication Code

macro Message1 = Na;
macro Message2 = Nb;
macro Message2-MAC = MAC(Nb, k(A,B));
macro Message3-Cipher = {A, B, D, Na}k(A, C);
macro Message3 = Message3-Cipher, A, Na, Nb; # Adding Nb achieves Nisynch and Niagree for A and C
macro Message3-MAC = MAC(Message3, k(A, B));
macro Message4 = (Message3-Cipher, B, Nb);
macro Message4-MAC = MAC(Message4, k(B, C));
macro Message5-Signed = {A, B, D}sk(C);
macro Message5 = {Message5-Signed, Pk, Sk, Nb, Nc}k(B, C); # Adding Nb achieves Nisynch and Niagree for B
macro Message6 = B, Na, Nc; # Adding Na achieves Weakagree for B and C in A
macro Message6-MAC = MAC(Message6, k(A,C));

protocol SAKES-AUTH(A, B, C, D) {
	role A {
		# 6LoWPAN End Device (A)

		fresh Na: Nonce;
		var Nb, Nc: Nonce;
		var Pk, Sk;

		send_1(A, B, Message1);
		recv_2(B, A, Message2);
		send_3(A, B, Message3, Message3-MAC);
		recv_6(C, A, Message6, Message6-MAC);

		claim(A, Alive, B); # Entity authentication of B
		claim(A, Alive, C); # Entity authentication of C
		claim(A, Weakagree, B); # Weak agreement of B
		claim(A, Weakagree, C); # Weak agreement of C
		claim(A, Niagree); # Non-injective agreement
		claim(A, Nisynch); # Non-injective synchronization
	}

	role B {
		# 6LoWPAN Router (B)

		fresh Nb: Nonce;
		var Na, Nc: Nonce;
		var Pk, Sk;

		recv_1(A, B, Na);
		send_2(B, A, Message2);
		recv_3(A, B, Message3, Message3-MAC);
		send_4(B, C, Message4, Message4-MAC);
		recv_5(C, B, Message5);

		claim(B, Secret, Sk); # Secrecy of the received ephemeral private key
		claim(B, Alive, A);
		claim(B, Alive, C);
		claim(B, Weakagree, A);
		claim(B, Weakagree, C);
		claim(B, Niagree);
		claim(B, Nisynch);
	}

	role C {
		# 6LoWPAN Border Router (C)

		fresh Nc: Nonce;
		var Na, Nb: Nonce;

		const Pk: Function;
		secret Sk: Function;
		inversekeys (Pk, Sk);

		recv_4(B, C, Message4, Message4-MAC);
		send_5(C, B, Message5);
		send_6(C, A, Message6, Message6-MAC);

		claim(C, Alive, A);
		claim(C, Alive, B);
		claim(C, Weakagree, A);
		claim(C, Weakagree, B);
		claim(C, Niagree);
		claim(C, Nisynch);
	}

	role D {
		# Remote Server (D)
	}
}
